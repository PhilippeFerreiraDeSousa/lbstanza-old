defpackage stz/macroexpander :
  import core
  import collections
  import stz/macro-master
  import stz/core-macros

;============================================================
;====================== API =================================
;============================================================

;Utility type that can expand macros.
public deftype Macroexpander

;Expand the given form.
public defmulti macroexpand (e:Macroexpander, form) -> ?

;Register a macro plugin.
;If a plugin is loaded, macroexpansion will be done by going through
;the plugin.
public defmulti load-macro-plugin (e:Macroexpander, filename:String) -> False

;============================================================
;====================== Convenience =========================
;============================================================

public defn Macroexpander (plugin-name:String|False) -> Macroexpander :
  val expander = Macroexpander()
  match(plugin-name:String) :
    load-macro-plugin(expander, plugin-name)
  expander

;============================================================
;======================= Implementation =====================
;============================================================

public defn Macroexpander () -> Macroexpander :

  ;Holds the macro plugin if there is one.
  var plugin: MacroPlugin|False = false

  new Macroexpander :
  
    defmethod load-macro-plugin (this, filename:String) :
      plugin = load-macro-plugin(filename)

    defmethod macroexpand (this, form) :
      ;Case: There is a macro-plugin loaded, so use
      ;the plugins expansion feature.
      match(plugin:MacroPlugin) :
        macroexpand(plugin, form)

      ;Case: There is no macro-plugin loaded, so just
      ;expand the forms using the built-in Stanza
      ;core macros.
      else :
        parse-syntax[core / #exp](List(form))