defpackage stz/proj-env :
  import core
  import collections
  import stz/proj-ir
  import stz/proj-value-types

;Represents the environment in which to read the proj files.
public deftype ProjEnv 

;Retrieve the current working directory. This is used to resolve
;{WORKDIR} and to also compute absolute paths for everything.
public defmulti workdir (env:ProjEnv) -> String

;Retrieve the parameter names and types for the given package manager.
public defmulti foreign-package-managers (env:ProjEnv) -> Tuple<ForeignPackageManagerInfo> :
  []

;Retrieve the additional build variables computed by the foreign package managers.
;A returned value must satisfy:
;  value = Symbol
;        | String
;        | List<value>
;and it will be translated over to a ProjValue before substitution.
public defmulti package-manager-variables (env:ProjEnv,                                           
                                           params:ForeignPackageParamsStmt,
                                           platform:Symbol)
                                        -> Tuple<KeyValue<Symbol, ?>> :
  []

;Contains the information about a foreign package manager.
;- name: The name of the package manager. e.g. `conan
;- params: The list of parameters that the package manager can
;  be configured with.
public defstruct ForeignPackageManagerInfo :
  name:Symbol
  params:Tuple<FieldType>

;The foreign package manager communicates the types of its
;parameters using a Tuple<FieldType>.
;- field-name: The name of the field.
;- optional?: Whether the field is optional.
;- field-type: The type of the field.
public defstruct FieldType :
  field-name: Symbol
  optional?: True|False
  field-type: ValueType
  field-context: FieldContext with:
    default => StandardField

;Literals behave differently depending on the statement
;in which they appear.
public defenum FieldContext :
  ;Paths are calculated relative to the proj file in which
  ;they appear. Prefixes are automatically added to paths. 
  PathField

  ;Commands are handled specially because a command that
  ;originates as a single string must remain as a single
  ;string, no matter what values are spliced into the command
  ;via variables.
  CommandField

  ;Standard fields undergo no preprocessing in the reader.  
  StandardField